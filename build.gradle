import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2" }
        maven { url "https://maven.minecraftforge.net" }
        maven { url "https://repo.spongepowered.org/repository/maven-public" }
    }
    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
        classpath "net.minecraftforge.gradle:ForgeGradle:5.1.+"
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
    }
}

apply plugin: "java"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "net.minecraftforge.gradle"
apply plugin: "org.spongepowered.mixin"

def travisBuildNumber = System.getenv("TRAVIS_BUILD_NUMBER")
def versionSuffix = travisBuildNumber != null ? travisBuildNumber : "SNAPSHOT"

version "1.1.1-$versionSuffix"
group "org.dimdev.vanillafix"
archivesBaseName = "VanillaFix"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    mappings channel: "stable", version: "39-1.12"

    accessTransformer = file('src/main/resources/META-INF/vanillafix_at.cfg')

    runs {
        client {
            jvmArg "-Dfml.coreMods.load=org.dimdev.vanillafix.VanillaFixLoadingPlugin"
            jvmArg "-Dmixin.hotSwap=true"
            jvmArg "-Dmixin.checks.interfaces=true"
            jvmArg "-Dmixin.debug.export=true"
            property "forge.logging.console.level", "debug"
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
            workingDirectory project.file("run")
        }

        server {
            jvmArg "-Dfml.coreMods.load=org.dimdev.vanillafix.VanillaFixLoadingPlugin"
            jvmArg "-Dmixin.hotSwap=true"
            jvmArg "-Dmixin.checks.interfaces=true"
            jvmArg "-Dmixin.debug.export=true"
            property "forge.logging.console.level", "debug"
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
            workingDirectory project.file("run")
        }
    }
}

mixin {
    add sourceSets.main, "mixins.vanillafix.refmap.json"
}

sourceSets {
    api
    main
}

configurations {
    compileJar {
        implementation.extendsFrom(compileJar)
    }
}

repositories {
    maven { url "https://repo.spongepowered.org/repository/maven-public" }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
        metadataSources {
            artifact()
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.12.2-14.23.5.2860"
    compileOnly sourceSets.api.output

    annotationProcessor("org.spongepowered:mixin:0.8.3:processor")
    compileJar("org.spongepowered:mixin:0.8.3") {
        exclude module: "asm-commons"
        exclude module: "asm-tree"
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "log4j-core"
        exclude module: "gson"
        exclude module: "commons-io"
    }
    compileOnly fg.deobf( "curse.maven:avaritia-1-10-261348:3143349" )
    compileOnly fg.deobf( "curse.maven:chisel-235279:2535884" )
    compileOnly fg.deobf( "curse.maven:codechicken-lib-1-8-242818:2779848" )
    compileOnly fg.deobf( "curse.maven:ctm-267602:2915363" )
    compileOnly fg.deobf( "curse.maven:custom-main-menu-226406:2857869" )
    compileOnly fg.deobf( "curse.maven:openblocks-228816:2699056" )
    compileOnly fg.deobf( "curse.maven:openmodslib-228815:2699055" )
    compileOnly fg.deobf( "curse.maven:tinkers-construct-74072:2902483" )
}

jar {
    manifest {
        attributes([
                "FMLCorePluginContainsFMLMod": "true",
                "FMLCorePlugin": "org.dimdev.vanillafix.VanillaFixLoadingPlugin",
                "FMLAT": "vanillafix_at.cfg"
        ])
    }
}

java {
    withSourcesJar()
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    from (sourceSets.main.resources.srcDirs) {
        include("mcmod.info")
        expand("version": version)
    }
}

shadowJar {
    archiveClassifier.set(null)
    configurations = [project.configurations.compileJar]
}

task thinShadowJar(type: ShadowJar) {
    archiveClassifier.set("thin")
    configurations = [project.configurations.shadow]
    relocators = shadowJar.relocators

    dependencies {
        exclude(dependency("org.spongepowered:mixin"))
    }

    manifest {
        inheritFrom jar.manifest
    }

    from sourceSets.main.output
}

artifacts {
    archives shadowJar
    archives sourcesJar
    archives thinShadowJar
}

reobf {
    shadowJar {}
    thinShadowJar {}
}