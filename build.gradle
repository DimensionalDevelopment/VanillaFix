plugins {
	id "fabric-loom" version "0.8-SNAPSHOT"
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = computeVersion(project.mod_version)

static def computeVersion(String version) {
	if (version.contains("beta")) {
		return version + "-" + Calendar.getInstance().getTime().format("dd-MM-yyyy")
	}
	return version
}

repositories {
	//mavenCentral()
	maven { url "https://maven.shedaniel.me" }
	maven { url "https://maven.terraformersmc.com" }
	maven { url "https://api.modrinth.com/maven" }
	maven { url "https://jitpack.io" }
}

minecraft {
	accessWidener "src/main/resources/vanillafix_aw.txt"
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Cos why not
	modImplementation "com.github.Chocohead:Fabric-ASM:v${project.fabric_asm_version}"
	include "com.github.Chocohead:Fabric-ASM:v${project.fabric_asm_version}"

	// Compatibility
	modImplementation "maven.modrinth:lithium:mc${project.minecraft_version}-${project.lithium_version}"
	// Phosphor is not yet updated.
	// modImplementation "maven.modrinth:phosphor:mc${project.minecraft_version}-${project.phosphor_version}"

	// Fun
	modRuntime "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"

	// Config
	modImplementation "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}"
	include "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}"
	modImplementation "com.terraformersmc:modmenu:${project.mod_menu_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

java {
	withSourcesJar()
}

jar {
	from "LICENSE"
}
