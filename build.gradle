buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2" }
        maven { url "https://maven.minecraftforge.net" }
        maven { url "https://repo.spongepowered.org/repository/maven-public" }
    }
    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
        classpath "net.minecraftforge.gradle:ForgeGradle:5.1.+"
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
    }
}

apply plugin: "java"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "net.minecraftforge.gradle"
apply plugin: "org.spongepowered.mixin"

def travisBuildNumber = System.getenv("TRAVIS_BUILD_NUMBER")
def versionSuffix = travisBuildNumber != null ? travisBuildNumber : "SNAPSHOT"

version "1.1.1-$versionSuffix"
group "org.dimdev.vanillafix"
archivesBaseName = "VanillaFix"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    mappings channel: "stable", version: "39-1.12"

    accessTransformer = file('src/main/resources/META-INF/vanillafix_at.cfg')

    runs {
        client {
            jvmArg "-Dfml.coreMods.load=org.dimdev.vanillafix.VanillaFixLoadingPlugin"
            jvmArg "-Dmixin.hotSwap=true"
            jvmArg "-Dmixin.checks.interfaces=true"
            jvmArg "-Dmixin.debug.export=true"
            property "forge.logging.console.level", "debug"
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
            workingDirectory project.file("run")
        }

        server {
            jvmArg "-Dfml.coreMods.load=org.dimdev.vanillafix.VanillaFixLoadingPlugin"
            jvmArg "-Dmixin.hotSwap=true"
            jvmArg "-Dmixin.checks.interfaces=true"
            jvmArg "-Dmixin.debug.export=true"
            property "forge.logging.console.level", "debug"
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
            workingDirectory project.file("run")
        }
    }
}

mixin {
    add sourceSets.main, "mixins.vanillafix.refmap.json"
}

sourceSets {
    main
}

repositories {
    maven { url "https://repo.spongepowered.org/repository/maven-public" }
    maven { url "http://chickenbones.net/maven/" }
    maven { url "http://www.dimdev.org/maven" }
    maven { url "http://maven.tterrag.com/" }
    ivy {
        url "https://google.com/404"
        ivyPattern("https://google.com/404") // Workaround for https://github.com/gradle/gradle/issues/4107
        artifactPattern("https://media.forgecdn.net/files/[organisation]/[module]/[revision].[ext]")
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.12.2-14.23.5.2860"

    annotationProcessor("org.spongepowered:mixin:0.8.3:processor")
    implementation("org.spongepowered:mixin:0.8.3") {
        exclude module: "asm-commons"
        exclude module: "asm-tree"
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "log4j-core"
        exclude module: "gson"
        exclude module: "commons-io"
    }
    compileOnly "2535:884:Chisel-MC1.12.2-0.2.0.31@jar"
    compileOnly "2530:225:OpenBlocks-1.12.2-1.7.6@jar"
    compileOnly "2525:230:OpenModsLib-1.12.2-0.11.5@jar"
    compileOnly "2568:0:TConstruct-1.12.2-2.10.1.84@jar"
    compileOnly "2568:0:TConstruct-1.12.2-2.10.1.84@jar"
    compileOnly "2511:625:Avaritia-1.12-3.2.0.13-universal@jar"
    compileOnly "codechicken:CodeChickenLib:1.12.2-3.1.9.344:deobf"
    compileOnly "2482:584:CustomMainMenu-MC1.12.2-2.0.8@jar"
    compileOnly "team.chisel.ctm:CTM:MC1.12-0.2.3.12"
}

jar {
    classifier "thin"
    manifest.attributes(
            "FMLCorePlugin": "org.dimdev.vanillafix.VanillaFixLoadingPlugin",
            "FMLCorePluginContainsFMLMod": "true",
            "FMLAT": "vanillafix_at.cfg"
    )
}

java {
    withSourcesJar()
}

processResources {
    filesMatching("mcmod.info") {
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // Re-run this task when these change
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
}

shadowJar {
    classifier ""
    exclude "LICENSE.txt", "dummyThing"
}

artifacts {
    archives jar
    archives shadowJar
    archives sourcesJar
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}

// These are broken
runClient.group "other"
runServer.group "other"